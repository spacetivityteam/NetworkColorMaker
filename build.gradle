allprojects {
    group 'net.spacetivity.color'
    version '1.0'

    repositories {
        maven {
            url 'https://nexus.neptuns-world.com/repository/maven-group/'
            credentials {
                username "${nexusUsername}"
                password "${nexusPassword}"
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    dependencies {
        implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.0'
        implementation group: 'org.redisson', name: 'redisson', version: '3.16.7'
        implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.0.2-rc'
        compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    test {
        useJUnitPlatform()
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    tasks.withType(JavaCompile).configureEach {
        javaCompiler = javaToolchains.compilerFor {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier.set('javadoc')
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        manifest {
            attributes(
                    'Implementation-Version': project.version,
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }

        repositories {
            maven {
                if (project.version.endsWith("SNAPSHOT")) {
                    url 'https://nexus.neptuns-world.com/repository/maven-snapshots/'
                } else {
                    url 'https://nexus.neptuns-world.com/repository/maven-releases/'
                }

                credentials {
                    username "${nexusUsername}"
                    password "${nexusPassword}"
                }
            }
        }
    }
}